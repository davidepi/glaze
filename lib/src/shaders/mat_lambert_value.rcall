#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier: require

#include "constants.glsl"
#include "spectrum.glsl"
#include "raytrace_structures.glsl"
#include "raytrace_commons.glsl"

layout(location = 0) callableDataInEXT BsdfValue bsdf;

layout(std430, set=1, binding=4) readonly buffer materialBuffer {
  RTMaterial materials[];
} MaterialBuffer;

layout(set=1, binding=6) uniform sampler2D textures[];

float lambert_pdf(in vec3 wo, in vec3 wi)
{
  return abs(wi.z) * INV_PI;
}

void main()
{
  vec3 wo = to_shading_space(bsdf.woW, bsdf.shading);
  vec3 wi = to_shading_space(bsdf.wiW, bsdf.shading);
  float same_hemi = step(0.0, wo.z*wi.z);
  RTMaterial mat = MaterialBuffer.materials[bsdf.material_index];
  float value = INV_PI;
  vec3 sampled_texel = texture(textures[nonuniformEXT(mat.diffuse)], bsdf.uv).rgb;
  vec3 diffuse_mul = mat.diffuse_mul.xyz;
  bsdf.value = from_surface_color(sampled_texel*diffuse_mul*value);
  bsdf.pdf = same_hemi*abs(wi.z)*INV_PI;
}

#version 460
#extension GL_EXT_ray_tracing : require

#include "spectrum.glsl"
#include "shading_space.glsl"
#include "fresnel.glsl"
#include "raytrace_structures.glsl"
#include "raytrace_commons.glsl"

layout(location = 0) callableDataInEXT BsdfSampleValue bsdf;

layout(std430, set=1, binding=4) readonly buffer materialBuffer {
  RTMaterial materials[];
} MaterialBuffer;

Spectrum mirror_sample_value(in vec3 wo, out vec3 wi, out float pdf)
{
  wi.x = -wo.x;
  wi.y = -wo.y;
  wi.z = wo.z;
  RTMaterial mat = MaterialBuffer.materials[bsdf.material_id];
  Spectrum fresnel = fresnel_conductor(wo.z, mat.metal_ior, mat.metal_fresnel);
  pdf = 1.0;
  return div(fresnel, abs(wo.z));
}

void main()
{
  vec3 wo = to_shading_space(bsdf.woW, bsdf.shading);
  vec3 wi;
  Spectrum value = mirror_sample_value(wo, wi, bsdf.pdf);
  bsdf.wiW = normalize(to_world_space(wi, bsdf.shading));
  bsdf.value = value;
}
